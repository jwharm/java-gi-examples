plugins {
    id 'application'
    id 'io.github.jwharm.flatpak-gradle-generator' version "1.5.0"
}

repositories {
    maven { url='build/repository' } // used by flatpak-builder
}

dependencies {
    implementation 'org.java-gi:adw:0.13.1'
}

// Task to compile gresource files
tasks.register('compileResources', Exec) {
    workingDir 'src/main/resources'
    commandLine 'glib-compile-resources', 'helloworld.gresource.xml'
}

// Task to generate a file with all dependency urls for the offline flatpak build
tasks.flatpakGradleGenerator {
    outputFile.set(file("$rootDir/flatpak/maven-dependencies.json"))
    downloadDirectory.set('build/repository')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

tasks.named('compileJava') {
    dependsOn compileResources
}

tasks.named('installDist') {
    destinationDir = file('/app/HelloTemplate')
}

application {
    mainClass = "my.example.hellotemplate.Main"
    applicationDefaultJvmArgs += "--enable-native-access=ALL-UNNAMED"
}
